program        → declaration* EOF ;
declaration    → statement ;
varDeclStmt   → "let" declExpr ( "," declExpr )* ";" ;
varDecl        → "let" declExpr ;
declExpr       → IDENT ( "=" expression )?;
statement      → exprStmt | printStmt | varDeclStmt | block ;
block          → "{" declaration* "}" ;
exprStmt       → expression ( "," expression)* ";" ;
printStmt      → "print" expression ";" ;
expression     → assignment | varDecl ;
assignment     → IDENT "=" assignment | ternary ;
ternary        → equality ( "?" ternary ":" ternary )? ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → unary ( ( "/" | "*" ) unary )* ;
unary          → ( "!" | "-" ) unary
               | primary ;
primary        → NUMBER | STRING | "(" expression ")" | IDENT ;